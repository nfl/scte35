buildscript {
    repositories {
        maven {
            url "http://nexus.la3.nfl.com/nexus/content/groups/public"
        }
        mavenCentral()
    }
    dependencies {
        classpath 'com.nfl.dm.shield.gradle:shield-plugin:1.0.+'
    }

}
apply plugin: 'com.nfl.dm.shield.gradle'
apply plugin: 'java'

targetCompatibility = '1.7'
sourceCompatibility = '1.7'


repositories {
    mavenCentral()
}

dependencies {
    compile 'commons-codec:commons-codec:1.10'
    testCompile 'org.testng:testng:6.8.8'
}


def _versionCode = getVersionCode()
def _versionName = getVersionName(_versionCode)

group 'com.nfl.dm.scte35'
version  = _versionName

int getVersionCode() {
    def gitCount = execute('git', 'rev-list', '--count', 'HEAD')
    return Integer.parseInt(gitCount);
}

String getVersionName(int versionCode) {
    
    def jenkinsHome = System.getenv("JENKINS_HOME")

    // Local (developer) build, e.g. "0.1.branch-SNAPSHOT"
    if(jenkinsHome == null || jenkinsHome == "") {
        def gitBranch = execute('git', 'symbolic-ref', '--short', 'HEAD')
        return "${BASE_VERSION}.${gitBranch}-SNAPSHOT"
    }

    // Jenkins CI build, e.g. "0.1.D18923-SNAPSHOT"
    def phabid = execute("./phabid.sh")
    if(phabid != "") {
        return "${BASE_VERSION}.${phabid}-SNAPSHOT"
    }

    // Jenkins master build, e.g., "0.1.10"
    return "${BASE_VERSION}.${versionCode}"
}

def execute(String cmd, String... arguments) {
    def stdout = new ByteArrayOutputStream()
    exec {
        workingDir "${rootDir}"
        executable "${cmd}"
        args(arguments)
        standardOutput = stdout
    }
    return stdout.toString().trim()
}